include 'skin/c'
include 'arch/c'
include 'util/for'
include 'util/kind'

def ux = u64
config usz = u32

def same = is
oper ~~ reinterpret infix right 55
oper ** broadcast infix right 55

def isreg   = kreg
def isconst = kcon
def istype  = ktyp
def istup   = ktup

def isunsigned{T} = isint{T} & ~issigned{T}

def isvec {T} = 0; def isvec {T & istype{T}} = same{typekind{T},'vector'}
def isprim{T} = 0; def isprim{T & istype{T}} = same{typekind{T},'primitive'}
def isptr {T} = 0; def isptr {T & istype{T}} = same{typekind{T},'pointer'}
def elwidth{T} = width{eltype{T}}

oper &~ andnot infix none 35
def andnot{a, b & anyNum{a} & anyNum{b}} = a & ~b



def load{p:P,  n    & isvec{eltype{P}}} = assert{0}
def store{p:P, n, v & isvec{eltype{P}}} = assert{0}
def load{p:P & isptr{P}} = load{p, 0}
# def store{p:P, v & isptr{P}} = store{p, 0, v}
def loadu{p:T               & isunsigned{eltype{T}}} = emit{eltype{T}, merge{'loadu_u',fmtnat{elwidth{T}}}, p}
def storeu{p:T, v:eltype{T} & isunsigned{eltype{T}}} = emit{void,     merge{'storeu_u',fmtnat{elwidth{T}}}, p, v}
def loadu{p:T               & issigned{eltype{T}}} = loadu {*ty_u{eltype{T}} ~~ p}
def storeu{p:T, v:eltype{T} & issigned{eltype{T}}} = storeu{*ty_u{eltype{T}} ~~ p, ty_u{v}}
def loadu{p:T               & elwidth{T}==8} = load{p}
def storeu{p:T, v:eltype{T} & elwidth{T}==8} = store{p, v}


def reinterpret{T, x:X & T==X} = x
def exportN{f, ...ns} = each{export{.,f}, ns}
def exportT{name, fs} = { v:*type{tupsel{0,fs}} = fs; export{name, v} }


# hints
def rare{x & knum{x}} = x
def rare{x:u1} = emit{u1, '__builtin_expect', x, 0}
def assert{x & x==0} = assert{'failed assertion'}
def assert{x & x==1} = 1
def unreachable{} = emit{void, 'si_unreachable'}
def assert{x:u1} = { if (not x) emit{void, 'si_unreachable'} }

# various checks
def oneVal{{h, ...t}} = {
  each{{c} => assert{c==h}, t}
  h
}
def oneVal{{}} = {}
def oneType{x} = oneVal{each{type, x}}

def anyNum{x} = isconst{x} | knum{x}
def anyNum{x:T} = isprim{T}

def anyInt{x} = 0
def anyInt{x & knum{x}} = (x>>0) == x
def anyInt{x & isreg{x}|isconst{x}} = isint{x}

# vector width/type checks
def w64 {T}   = 0; def w64 {T   & isvec{T}} = width{T}==64
def w128{T}   = 0; def w128{T   & isvec{T}} = width{T}==128
def w256{T}   = 0; def w256{T   & isvec{T}} = width{T}==256
def w64 {T,w} = 0; def w64 {T,w & w64{T}}   = elwidth{T}==w
def w128{T,w} = 0; def w128{T,w & w128{T}}  = elwidth{T}==w
def w256{T,w} = 0; def w256{T,w & w256{T}}  = elwidth{T}==w

# width+type checks
def genchk{B, F} = {
  def r{T} = 0
  def r{T & B{T}} = F{eltype{T}}
  def r{T,w} = 0
  def r{T,w & B{T}} = F{eltype{T}} & (elwidth{T}==w)
  def r{T & ~isvec{T}} = 0
  r
}
def w256i = genchk{w256,      isint};  def w128i = genchk{w128,      isint};  def w64i = genchk{w64,      isint}
def w256s = genchk{w256,   issigned};  def w128s = genchk{w128,   issigned};  def w64s = genchk{w64,   issigned}
def w256u = genchk{w256, isunsigned};  def w128u = genchk{w128, isunsigned};  def w64u = genchk{w64, isunsigned}
def w256f = genchk{w256,    isfloat};  def w128f = genchk{w128,    isfloat};  def w64f = genchk{w64,    isfloat}



def trunc{T, x:U & isint{T} & isint{U} & T<=U} = emit{T, '', x}
def trunc{T, x & knum{x}} = cast{T, x}

def tern{c, T, F & anyInt{c}} = {
  if(c) T
  else F
}
def tern{c, t:T, f:T & anyInt{c}} = {
  res:T = f
  if (c) res = t
  res
}

def to_w{T, w} = primtype{quality{T}, w}

def re_el{E, V} = [width{V}/width{E}]E
def re_el{E, x:V} = re_el{E,V} ~~ x

local def qualChange{q} = {
  def f{w & knum{w}} = primtype{q, w}
  def f{T & isprim{T}} = primtype{q, width{T}}
  def f{T & isvec{T}} = re_el{f{eltype{T}}, T}
  def f{x:T} = f{T}~~x
}
def ty_u = qualChange{'u'}
def ty_s = qualChange{'i'}
def ty_f = qualChange{'f'}

def w_n{T, w & isprim{T}} = primtype{quality{T}, w}
def w_d{T & isprim{T}} = to_w{T, width{T}*2} # double/halve primitive type width
def w_h{T & isprim{T}} = to_w{T, width{T}/2}

def n_d{T & isvec{T}} = [vcount{T}*2](eltype{T}) # double/halve vector count
def n_h{T & isvec{T}} = [vcount{T}/2](eltype{T})

def el_d{T & isvec{T}} = [vcount{T}](w_d{eltype{T}}) # double/halve element width, preserving count
def el_h{T & isvec{T}} = [vcount{T}](w_h{eltype{T}})

def el_m{T & isvec{T}} = re_el{w_d{eltype{T}}, T};  def el_m{x:T} = re_el{T}~~x # double/halve element width, preserving width
def el_s{T & isvec{T}} = re_el{w_h{eltype{T}}, T};  def el_s{x:T} = re_el{T}~~x

# type stats
def minvalue{T & isunsigned{T}} = 0
def maxvalue{T & isunsigned{T}} = (1<<width{T})-1
def minvalue{T & issigned{T}} = - (1<<(width{T}-1))
def maxvalue{T & issigned{T}} = (1<<(width{T}-1))-1




# tuple operations
def iota{n & knum{n}} = range{n}
def broadcast{T, v & isprim{T}} = v
def broadcast{n, v & knum{n}} = each{{_}=>v, range{n}}
def collect{vars,begin,end,iter & knum{begin} & knum{end}} = {
  each{iter{., vars}, range{end-begin}+begin}
}

# convert tuple to number in little-endian base b
def base{b,l} = if (0==tuplen{l}) 0 else tupsel{0,l}+b*base{b,slice{l,1}}



# vector definitions
def arch_defvw = if (hasarch{'AVX2'}) 256 else 128
def has_simd = hasarch{'X86_64'} | hasarch{'AARCH64'}

# test if vector has a specific width & element type
def lvec{T, n, w} = 0
def lvec{T, n, w & isvec{T} & vcount{T}==n & elwidth{T}==w} = 1

# base cases
def {
  absu,andAllZero,andnz,b_getBatch,clmul,cvt,extract,fold_addw,half,
  homAll,homAny,homBlend,homMask,homMaskStore,homMaskStoreF,loadBatchBit,
  loadLow,make,maskStore,maskToHom,mulw,mulh,narrow,narrowPair,packHi,packLo,packQ,pair,pdep,
  pext,popcRand,sel,shl,shr,shuf,shuf16Hi,shuf16Lo,shufHalves,storeLow,
  topBlend,topMask,topMaskStore,topMaskStoreF,unord,unpackQ,vfold,widen,
  zip,zipHi,zipLo
}

def homMaskX{a:T} = tup{1, homMask{a}} # tup{n,mask}; mask with each bit repeated n times
def ctzX{{n,v}} = ctz{v}/n # ctz for a result of homMaskX
def homMask{...vs & tuplen{vs}>1} = {
  def n = tuplen{vs}
  def T = oneType{vs}
  def RT = ty_u{max{8,vcount{T}*n}}
  def a = promote{RT, homMask{...slice{vs,0,n/2}}}
  def b = promote{RT, homMask{...slice{vs,n/2}}}
  (b<<(n/2 * vcount{T})) | a
}

if (hasarch{'X86_64'}) {
  include 'arch/iintrinsic/basic'
  include './sse2'
  include './sse'
  include './avx'
  include './avx2'
} else if (hasarch{'AARCH64'}) {
  include 'arch/neon_intrin/basic'
  include './neon'
} else {
  def {__adds,__subs,__sqrt,vec_broadcast,vec_make}
}

def mzip  {a:T, b:T} = each{{v}=>el_m{T}~~v, zip{a, b}}
def mzipLo{a:T, b:T} = el_m{T} ~~ zipLo{a, b}
def mzipHi{a:T, b:T} = el_m{T} ~~ zipHi{a, b}
def packQ{{a, b}} = packQ{a, b}
def pair{{a, b}} = pair{a, b}

def widen{T, x:X & T==X} = x
def narrow{T, x:X & T==eltype{X}} = x
def undefPromote{T, x:X & T==X} = x
def cvt{T, x:X & T==eltype{X}} = x

def broadcast{T, v & isvec{T}} = vec_broadcast{T, promote{eltype{T},v}}
def make{T, ...xs & isvec{T}} = vec_make{T, ...xs}
def iota{T & isvec{T}} = make{T, ...iota{vcount{T}}}
def absu{a:T & isvec{T}} = ty_u{abs{a}}

def floor = __floor
def ceil = __ceil
def abs = __abs
def min = __min
def max = __max
def adds = __adds
def subs = __subs
def sqrt = __sqrt



# more arith
def min{a, b & anyNum{a} & anyNum{b}} = tern{a<b, a, b}
def max{a, b & anyNum{a} & anyNum{b}} = tern{a>b, a, b}
def cdiv{a,b} = (a+b-1)/b # ceiling divide
def cdiv{a,b & knum{a} & knum{b}} = ceil{a/b}
def popc{x:T & isint{T} & width{T}==64} = emit{ux, '__builtin_popcountll', x}
def popc{x:T & isint{T} & width{T}<=32} = emit{ux, '__builtin_popcount', x}
def ctz{x:T & isint{T} & width{T}==64} = emit{ux, '__builtin_ctzll', x}
def ctz{x:T & isint{T} & width{T}<=32} = emit{ux, '__builtin_ctz', x}
def clz{x:T & isint{T} & width{T}==64} = emit{ux, '__builtin_clzll', x}
def clz{x:T & isint{T} & width{T}<=32} = emit{ux, '__builtin_clz', x}
# count-leading-zeros complement, less type-dependent
def clzc{x:T & isint{T} & width{T}==64} = 64-clz{x}
def clzc{x:T & isint{T} & width{T}<=32} = 32-clz{x}

def ceil_log2{n} = clzc{n-1}

def truncBits{n, v & n<=8}  = cast_i{u8, v}
def truncBits{n, v & n==16} = cast_i{u16, v}
def truncBits{n, v & n==32} = cast_i{u32, v}
def truncBits{n, v & n==64} = cast_i{u64, v}

# base-2 log of a constant power of two
def lb{n & knum{n} & (n>>1<<1) == n & n>0} = lb{n>>1}+1
def lb{n==1} = 0

def zlow{n,x} = (x >> n) << n    # zero out n least significant bits
def tail{n,x} = x & ((1<<n) - 1) # get the n least significant bits
def bit {k,x} = x &  (1<<k)      # get the k-th bit

# range logic
def inRangeLen{x:TS, start, count & issigned{eltype{TS}}} = { # ∊ [start;start+count)
  def TU = ty_u{TS}
  (TU~~(x-TS**start))  <  TU**count
}
def inRangeLen{x:TU, start, count & isunsigned{eltype{TU}}} = { # ∊ [start;start+count)
  def TS = ty_s{TU}
  def h = 1 << (elwidth{TU}-1)
  (TS~~(x-TU**(start-h)))  <  TS**(count-h)
}
def inRangeIncl{x:T, start, end} = inRangeLen{x, start, end-start+1} # ∊ [start;end]
def inRangeExcl{x:T, start, end} = inRangeLen{x, start, end-start} # ∊ [start;end)



def load{p,i & kgen{p}} = p{i}
def store{p,i,x & kgen{p}} = p{i,x}
def tptr{l,s} = { # create "pointer" generator with given load & store definitions
  def r{i} = l{i}
  def r{i,x} = s{i,x}
}

def unroll = for_const
def forNZ{vars,begin,end,iter} = {
  i:ux = begin
  assert{i!=end}
  while (i < end) {
    iter{i, vars}
    ++i
  }
}
def forUnroll{exp,unr}{vars,begin,end,iter} = {
  i:ux = begin
  while ((i+unr) <= end) {
    iter{each{{j}=>i+j, iota{unr}}, vars}
    i+= unr
  }
  if (unr==2) { if (i!=end) iter{tup{i}, vars} }
  else if (unr>1) {
    if (exp) {
      def stop = makelabel{}
      each{{j} => {
        if (i+j >= end) goto{stop}
        iter{tup{i+j}, vars}
      }, iota{unr}}
      setlabel{stop}
    } else {
      @for(j from i to end) iter{tup{j}, vars}
    }
  }
}

def makeBranch{Ts, F} = {
  def args = undef{Ts}
  def skip = makelabel{}; goto{skip}
  def start = setlabel{}
  F{...args}
  setlabel{skip}
  {...vs} => { each{=, args, vs}; goto{start} }
}
def makeOptBranch{enable, Ts, F} = if (enable) makeBranch{Ts, F} else 'not defined'


def tree_fold{F, x} = {
  def h = tuplen{x}>>1
  F{tree_fold{F, slice{x,0,h}}, tree_fold{F, slice{x,h,tuplen{x}}}}
}
def tree_fold{F, {x}} = x

def eachx{F, ...args} = {
  def l = tree_fold{max, each{{x} => if(ktup{x}) tuplen{x} else 0, args}}
  each{F, ...each{{x} => if (istup{x}) x else l**x, args}}
}

def undef{T, n & istype{T}} = @collect(n) undef{T}
def undef{Ts & istup{Ts}} = each{undef, Ts}
def undef{x:T} = undef{T}
def undef{T & istype{T}} = { reg:=undefined{T} }
